buildscript {
    repositories {
        maven { url "http://maven.xzlcorp.com/content/repositories/snapshots/" }
        maven { url "http://maven.xzlcorp.com/content/repositories/releases/" }
        maven { url "http://maven.aliyun.com/nexus/content/groups/public/" }
    }
    dependencies {
        classpath(
                "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}",
                'org.asciidoctor:asciidoctor-gradle-plugin:1.5.3'
        )
    }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'org.asciidoctor.convert'

//sourceCompatibility = 1.8
//targetCompatibility = 1.8

repositories {
    maven { url "http://maven.xzlcorp.com/content/repositories/snapshots/" }
    maven { url "http://maven.xzlcorp.com/content/repositories/releases/" }
    maven { url "http://maven.aliyun.com/nexus/content/groups/public/" }
}

dependencies {
    compile(
            'org.aspectj:aspectjweaver:1.8.11', // for java 9
            "cglib:cglib:3.2.5", // for java 9

            "javax.xml.bind:jaxb-api:2.1",//for java 9
            "com.sun.xml.bind:jaxb-impl:2.3.0",//for java 9
            "com.sun.xml.bind:jaxb-core:2.3.0",//for java 9

            "cn.xinzhili:x-utils-auth:${xutilsVersion}",
            "cn.xinzhili:x-utils-spring:${xutilsVersion}",
            "cn.xinzhili:user-service-api:${userServiceVersion}",
            'org.springframework.cloud:spring-cloud-starter-oauth2',
            'org.springframework.boot:spring-boot-starter-data-redis',
            'org.springframework.cloud:spring-cloud-starter-security',
            'org.springframework.security:spring-security-jwt',
            'org.springframework.cloud:spring-cloud-config-client',
            'org.springframework.cloud:spring-cloud-starter-eureka',
            'org.springframework.cloud:spring-cloud-starter-feign',
            'org.springframework.cloud:spring-cloud-starter-sleuth',
            'org.springframework.cloud:spring-cloud-starter-hystrix',
            'org.springframework.boot:spring-boot-starter-actuator',
            'org.jolokia:jolokia-core'
    )
    runtime(
            // sentry
            'com.getsentry.raven:raven-logback:7.8.0'
    )
    testCompile(
            'org.springframework.boot:spring-boot-starter-test',
            'org.springframework.restdocs:spring-restdocs-core:1.1.1.RELEASE',
            'org.springframework.restdocs:spring-restdocs-mockmvc:1.1.1.RELEASE'
    )
}

ext {
    snippetsDir = file('build/generated-snippets')
}

test {
    outputs.dir snippetsDir
}

asciidoctor {
    attributes 'snippets': snippetsDir
    inputs.dir snippetsDir
    dependsOn test
}

jar {
    dependsOn asciidoctor
    from("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.2.1'
    distributionUrl = 'http://get.jenv.mvnsearch.org/download/gradle/gradle-4.2.1.zip'
}

springBoot {
    buildInfo()
}
